plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

def javaProjects = [
        project(":order:order-domain"),
        project(":order:order-application"),
        project(":order:order-adapter")

]
def springBootProjects = [
        project(":order:order-application"),
        project(":order:order-adapter")
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"

    tasks.withType(JavaCompile) {
        sourceCompatibility = "17"
        targetCompatibility = "17"
        options.encoding = "UTF-8"
    }

    dependencies {
        implementation project(':infra')

        implementation 'org.hibernate:hibernate-validator:6.2.0.Final'
        implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
        implementation 'com.fasterxml.uuid:java-uuid-generator:4.1.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

        if (!springBootProjects.contains(project)) {
            testImplementation(platform('org.junit:junit-bom:5.7.1'))
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
            testImplementation 'org.assertj:assertj-core:3.20.2'
            testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
        }
    }
}

configure(springBootProjects) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework:spring-tx:6.0.9'

        implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.1")
        implementation 'io.awspring.cloud:spring-cloud-aws-starter'
        implementation 'io.awspring.cloud:spring-cloud-aws-starter-sns'
        implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'
        implementation 'software.amazon.awssdk:sqs:2.20.78'

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}

bootJar {
    enabled(false)
}

jar {
    enabled(true)
}